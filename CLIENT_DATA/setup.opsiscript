; This sourcecode published under the Terms of the General Public License.
; Written by Eric Esser, Wissenschaftszentrum Berlin, eric.esser@wzb.eu.
; Inspired by uib gmbh (www.uib.de), Thomas Besser (KIT), Detlef Krummel (GEI) and Tobias Friede (Fraunhofer)


[Actions]
;==========================================================================================================
; Includes
include_insert "common.opsiinc"

;==========================================================================================================
; Variables
DefVar $CustomPostInstall$
DefVar $SetupString$ 
DefVar $SetupParams$
DefVar $LicenseServer$
DefVar $IniFile$

Set $SetupString$ = "%ScriptPath%\" + $SetupFile$ 
Set $IniFile$ = "%CommonAppDataDir%\" + $VersionIdentifier$ + "\Config.ini"

;==========================================================================================================
; Program logic
if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError "No Space"
	; Stop process and set installation status to failed
else
	ChangeDirectory "%SCRIPTPATH%"

  if ($remove_old_versions$ = "true")
    Sub "%ScriptPath%\delsub.opsiscript"
  endif

	if (($KindOfLicense$ = "opsi-property") or ($KindOfLicense$ = "licensepool"))
	  Set $LicenseServer$ = GetProductProperty("licenseserver", "")
	  if (not($LicenseServer$ = ""))
	    Set $SetupParams$ = "LIC_SRV=" + $LicenseServer$ + " LIC_NAME=" + $LicenseKey$
	  else 
			LogError "No license server found in OPSI, though kind-of-license property set to opsi-property. Please enter valid license server in OPSI or change the kind of licensing."
      isFatalError "No license server found in OPSI, though kind-of-license property set to opsi-property"
	  endif
	endif
		
  if FileExists($SetupString$)
	  Message "Installing " + $ProductId$ + " ..."
	  comment "Start setup program"
    Winbatch_install
	  Sub_check_exitcode
	else
	  LogError "No setup file found, please check the OPSI package"
	  isFatalError "No Setup file found"
	endif

	if ($KindOfLicense$ = "ini-file-in-custom-folder") 
	  if (not(FileExists("%ScriptPath%\custom\Config.ini")))
	    LogError "Config.ini file not found, though kind-of-license is set to ini-file-in-custom-folder in the config editor."
	    isFatalError "Config.ini file not found."
	  endif	    
	  if (not(FileExists("%CommonAppDataDir%\"+ $VersionIdentifier$)))
      LogError "%CommonAppDataDir%\"+ $VersionIdentifier$ + " folder not found"
	    isFatalError "%CommonAppDataDir%\"+ $VersionIdentifier$ + " folder not found"
	  endif	
	  Files_copy_license
	endif

	; Setting the desktop icon or removing it - as set in the config editor
  if $Property_DesktopIcon$ = "True"
    Set $DesktopIconName$ = $PrettyName$
    LinkFolder_create
  else
    LinkFolder_delete
  endif

  comment "include custom post install file"
  set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
  if not ($CustomPostInstall$ = "none")
	  if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
		  include_insert "%ScriptPath%\custom\" + $CustomPostInstall$
	  endif
  endif

endif

;==========================================================================================================
; Functions

[Winbatch_install]
;msiexec /i "$SetupString$" /l* "$LogDir$\$ProductId$.install_log.txt" /qb! INSTALLDIR="$InstallDir$" ALLUSERS=1 REBOOT=ReallySuppress
msiexec /i "$SetupString$" /qn $SetupParams$ /l* "$LogDir$\$ProductId$.install_log.txt" DIR="$InstallDir$" ALLUSERS=1 REBOOT=ReallySuppress

[Files_copy_license]
copy "%ScriptPath%\custom\Config.ini" "%CommonAppDataDir%\$VersionIdentifier$"

[Patches_delete_inifile_license]
delsec [License]

[Patches_add_license_to_inifile]
set [License] LicNet = 1
set [License] LicSrv = $LicenseServer$
set [License] LicName = $LicenseKey$
