; This sourcecode published under the Terms of the General Public License.
; Written by Eric Esser, Wissenschaftszentrum Berlin, eric.esser@wzb.eu.
; Inspired by uib gmbh (www.uib.de), Thomas Besser (KIT), Detlef Krummel (GEI) and Tobias Friede (Fraunhofer)


[Actions]
;==========================================================================================================
; Includes
include_insert "common.opsiinc"

;==========================================================================================================
; Variables
DefVar $CustomPostInstall$
DefVar $SetupString$ 
DefVar $SetupParams$
DefVar $LicenseServer$

Set $SetupString$ = "%ScriptPath%\" + $SetupFile$ 


;==========================================================================================================
; Program logic
if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError "No Space"
	; Stop process and set installation status to failed
else
	ChangeDirectory "%SCRIPTPATH%"
  
  ;uninstall old version
  Sub "%ScriptPath%\delsub.opsiscript"    
  
	if ($KindOfLicense$ = "opsi-property")
	  Set $LicenseServer$ = GetProductProperty("licenseserver", "")
	  if (not($LicenseServer$ = ""))
	    Set $SetupParams$ = "LIC_SRV=" + $LicenseServer$ + " LIC_NAME=" + $LicenseKey$
	    comment "Licensing with opsi-property"
	  else 
			LogError "No license server found in OPSI, though kind-of-license property set to opsi-property. Please enter valid license server in the OPSI property or change the kind of licensing."
      isFatalError "No license server found in OPSI, though kind-of-license property set to opsi-property"
	  endif
	endif
	
	if ($KindOfLicense$ = "licensepool")
	  Set $LicenseServer$ = GetProductProperty("licenseserver", "")
	  if (not($LicenseServer$ = ""))
	    Set $SetupParams$ = "LIC_SRV=" + $LicenseServer$ + " LIC_NAME=" + $LicenseKey$
	    comment "Licensing with opsi licensepool"
	  else 
			LogError "No license server found in OPSI, though kind-of-license property set to licensepool. Please enter valid license pool in the OPSI property and a valid license in the license module or change the kind of licensing."
      isFatalError "No license server found in OPSI, though kind-of-license property set to licensepool"
	  endif
	endif
		
  if FileExists($SetupString$)
	  Message "Installing " + $ProductId$ + " ..."
	  comment "Start setup program"
    Winbatch_install
	  Sub_check_exitcode
	else
	  LogError "No setup file found, please check the OPSI package"
	  isFatalError "No Setup file found"
	endif

	if ($KindOfLicense$ = "ini-file-in-custom-folder") 
	  if (not(FileExists("%ScriptPath%\custom\Config.ini")))
	    LogError "Config.ini file not found, though kind-of-license is set to ini-file-in-custom-folder in the config editor."
	    isFatalError "Config.ini file not found."
	  endif	    
	  if (not(FileExists("%CommonAppDataDir%\"+ $VersionIdentifier$)))
      LogError "%CommonAppDataDir%\"+ $VersionIdentifier$ + " folder not found"
	    isFatalError "%CommonAppDataDir%\"+ $VersionIdentifier$ + " folder not found"
	  endif	
  	comment "Copying license file to %CommonAppDataDir%\" + $VersionIdentifier$
	  Files_copy_license
	endif

	; Setting the desktop icon or removing it - as set in the config editor
  if $Property_DesktopIcon$ = "True"
    Set $DesktopIconName$ = $PrettyName$
    LinkFolder_create
  else
    LinkFolder_delete
  endif

  comment "include custom post install file"
  set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
  if not ($CustomPostInstall$ = "none")
	  if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
		  include_insert "%ScriptPath%\custom\" + $CustomPostInstall$
	  endif
  endif

endif

;==========================================================================================================
; Functions

[Winbatch_install]
;msiexec /i "$SetupString$" /l* "$LogDir$\$ProductId$.install_log.txt" /qb! INSTALLDIR="$InstallDir$" ALLUSERS=1 REBOOT=ReallySuppress
msiexec /i "$SetupString$" /qn $SetupParams$ /l* "$LogDir$\$ProductId$.install_log.txt" DIR="$InstallDir$" ALLUSERS=1 REBOOT=ReallySuppress

[Files_copy_license]
copy "%ScriptPath%\custom\Config.ini" "%CommonAppDataDir%\$VersionIdentifier$"