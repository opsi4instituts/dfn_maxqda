; This sourcecode published under the Terms of the General Public License.
; Written by Eric Esser, Wissenschaftszentrum Berlin, eric.esser@wzb.eu.
; Inspired by uib gmbh (www.uib.de), Thomas Besser (KIT), Detlef Krummel (GEI) and Tobias Friede (Fraunhofer)

[Actions]
;==========================================================================================================
; Includes
include_insert "common.opsiinc"

;==========================================================================================================
; Variables
DefVar $CustomPostDeinstall$
;==========================================================================================================
; Program logic

Message "Uninstalling " + $ProductId$ + " ..."

Sub "%ScriptPath%\delsub.opsiscript"

; Free license if licensing module of OPSI is used
if (GetProductProperty("kind-of-license" , "testversion" ) = "licensepool")
	Sub_free_license  
  Comment "License freed."
endif

; Delete license files
if (FileExists($IniFile$))
  Comment "Deleting ini license file"
	Files_del_ini_file 
endif

if (FileExists($VlicFile$))
  Comment "Deleting vlic license file"
	Files_del_vlic_file 
endif


; Make sure the desktop icon gets deleted as well.
Set $DesktopIconName$ = $PrettyName$
LinkFolder_delete

; Include post deinstall file in custom folder via OPSI property 
comment "include custom post deinstall file"
Set $CustomPostDeinstall$ = getProductProperty("custom-post-deinstall","none")
if not ($CustomPostDeinstall$ = "none")
	if FileExists("%ScriptPath%\custom\" + $CustomPostDeinstall$)
		include_insert "%ScriptPath%\custom\" + $CustomPostDeinstall$
	endif
endif

[Files_del_ini_file]
del -f $IniFile$

[Files_del_vlic_file]
del -f $VlicFile$
